%header
{
    #include "ast.hpp"
    Node *parse(const char *filename);
}

%include
{
    #include <iostream>
    #include <string.h>
    #include "ast.hpp"
    #include "error.hpp"
    #include "symbolTable.hpp"

    extern int yylineno;

    Node *rootNode = new Node;
    SymbolTable symTable;
    
    std::string curFile;
    bool compileError = false;
}

%syntax_error
{
    error(curFile + ": Syntax error on line " + std::to_string(yylineno) + " near \""
            + TOKEN + "\".");
    compileError = true;
}

%parse_accept
{
    std::cout << "\033[96mParsing done.\033[39;49m\n";
}

%parse_failure
{
    error(curFile + ": Parsing failed.");
    compileError = true;
}

%stack_overflow
{
    error(curFile + ": Parser stack overflow.");
}

%token_prefix TOK_
%token_type   { const char * }

%left AND.
%left XOR.
%left OR.
%right NOT.

%type program { BlockNode * }
program ::= enterScope statements(B) exitScope. { rootNode = B; }
program ::= . // Empty file

// Block of statements
%type statements { BlockNode * }
statements(A) ::= statement(B).
{
    A = new BlockNode;
    A->statements.push_back(B);
}
statements(A) ::= statements(B) statement(C).
{
    B->statements.push_back(C);
    A = B;
}

%type statement { StatementNode * }
// Expression statement
statement(A) ::= expr(B) SEMICOLON. { A = new ExpressionStatementNode(*B); }

// Connection
statement(A) ::= ident(B) CONNECTION expr(D) SEMICOLON.
{
    if (symTable.isIdentInScope(B->ident))
    {
        Symbol ident = symTable.lookupIdent(B->ident);
        if (std::get<0>(ident) != OUTPUT)
        {
            compileError = true;
            error(curFile + ": Type error near line " + std::to_string(yylineno) +
                    ": Identifier, \"" + B->ident +
                    "\", must be an output.");
        }
    }
    else
    {
        compileError = true;
        error(curFile + ":  Scope error near line " + std::to_string(yylineno) +
                ": Identifier, \"" + B->ident + "\", not in scope.");
    }
    A = new ExpressionStatementNode(*(new BinaryOperationNode("con", *B, *D)));
}

// Module instantiation
statement(A) ::= ident(B) ident(C) COLON argList(D) SEMICOLON.
{
    // If the instance ident is already in scope
    if (symTable.isIdentInScope(C->ident))
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Redefinition of identifier \"" + C->ident + "\".");
    }
    else
    {
        if (symTable.isIdentInScope(B->ident)) // If the module ident is in scope
        {
            Symbol module = symTable.lookupIdent(B->ident);
            // If the module ident is not a module
            if (std::get<0>(module) != MODULE)
            {
                compileError = true;
                error(curFile + ": Type error near line " + std::to_string(yylineno) +
                        ": Identifier, \"" + B->ident + "\", is not a module.");
            }
            else if (D.size() != std::get<2>(module))
            {
                compileError = true;
                error(curFile + ": Module instantiation error near line " +
                        std::to_string(yylineno) +
                        ": Too many or not enough arguments.");
            }

        }
        else
        {
            compileError = true;
            error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                    ": Module, \"" + B->ident + "\", not in scope.");
        }
        symTable.insertSym(MODULE_INST, C->ident);
    }
    A = new ModuleInstanceNode(*C, *B, D);
}

// Argument list for module instances
%type argList { std::vector<IdentifierNode *> }
argList(A) ::= ident(B).
{
    if (!symTable.isIdentInScope(B->ident))
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Identifier, \"" + B->ident + "\", not in scope.");
    }
    A = std::vector<IdentifierNode *>();
    A.push_back(B);
}
argList(A) ::= argList(B) COMMA ident(C).
{
    if (!symTable.isIdentInScope(C->ident))
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Identifier, \"" + C->ident + "\", not in scope.");
    }
    B.push_back(C);
    A = B;
}

// Module definition
statement(A) ::= MODULE enterScope ident(B) parameterList(C) SEMICOLON
        statements(D) END exitScope.
{
    if (!symTable.isIdentInScope(B->ident)) // If the ident is not in scope
    {
        symTable.insertSym(MODULE, B->ident, C.size());
    }
    else
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Redefinition of module \"" + B->ident + "\".");
    }
    A = new ModuleDefinitionNode(*B, C, *D);
}
// Empty module
statement ::= MODULE enterScope ident parameterList SEMICOLON END exitScope.
{
    compileError = true;
    error(curFile + ": Error near line " + std::to_string(yylineno) +
            ": Cannot define empty modules.");
}


// Module parameters
%type parameterList { std::vector<Parameter *> }
parameterList(A) ::= . { A = std::vector<Parameter *>(); }
parameterList(A) ::= parameter(B).
{
    A = std::vector<Parameter *>();
    A.push_back(B);
}
parameterList(A) ::= parameterList(B) COMMA parameter(C).
{
    B.push_back(C);
    A = B;
}

%type parameter { Parameter * }
parameter(A) ::= INPUT ident(B).
{
    if (!symTable.isIdentInScope(B->ident))
    {
        symTable.insertSym(INPUT, B->ident);
        A = new Parameter(INPUT, B);
    }
    else
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Redefinition of identifier \"" + B->ident + "\".");
    }

}
parameter(A) ::= OUTPUT ident(B). 
{
    if (!symTable.isIdentInScope(B->ident))
    {
        symTable.insertSym(OUTPUT, B->ident);
        A = new Parameter(OUTPUT, B);
    }
    else
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Redefinition of identifier \"" + B->ident + "\".");
    }
}

// Expression
%type expr { ExpressionNode * }
expr(A) ::= LBRACKET expr(B) RBRACKET. { A = B; }
expr(A) ::= expr(B) AND expr(D). { A = new BinaryOperationNode("and", *B, *D); }
expr(A) ::= expr(B) XOR expr(D). { A = new BinaryOperationNode("xor", *B, *D); }
expr(A) ::= expr(B) OR expr(D).  { A = new BinaryOperationNode("or", *B, *D); }
expr(A) ::= NOT expr(C).         { A = new UnaryOperationNode("not", *C); }
expr(A) ::= TRUE.                { A = new BoolNode(true); }
expr(A) ::= FALSE.               { A = new BoolNode(false); }
expr(A) ::= ident(B).            
{
    if (symTable.isIdentInScope(B->ident))
    {
        if (std::get<0>(symTable.lookupIdent(B->ident)) == OUTPUT)
        {
            compileError = true;
            error(curFile + ": Type error near line " + std::to_string(yylineno) +
                    ": Cannot use an output as a value.");
        }
    }
    else
    {
        compileError = true;
        error(curFile + ": Scope error near line " + std::to_string(yylineno) +
                ": Identifier, \"" + B->ident + "\", not in scope.");
    }
    A = B;
}

%type ident { IdentifierNode * }
ident(A) ::= IDENT(B). { A = new IdentifierNode(B); }

// Scope rules
enterScope ::= . { symTable.enterScope(); }
exitScope ::= .  { symTable.exitScope(); }

%code
{
    extern int yylex();
    extern char *yytext;
    extern FILE *yyin;

    Node *parse(const char *filename)
    {
        yyin = fopen(filename, "r"); // Open file
        void *parser = ParseAlloc(malloc);
        int token;

        curFile = filename;

        // Uncomment for debugging
        // ParseTrace(stdout, "Parser: ");

        while ((token = yylex()))
            Parse(parser, token, strdup(yytext));

        Parse(parser, 0, NULL);
        ParseFree(parser, free);

        if (compileError == true)
            exit(EXIT_FAILURE);
        
        fclose(yyin); // Close file
        return rootNode;
    }
}
